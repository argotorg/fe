Added the `mut` keyword, to mark things as mutable. Any variable or function parameter
not marked `mut` is now immutable.

```fe
contract Counter {
    count: u256

    pub fn increment(mut self) -> u256 {
        // `self` is mutable, so storage can be modified
        self.count += 1
        return self.count
    }
}

struct Point {
    pub x: u32
    pub y: u32

    pub fn add(mut self, _ other: Point) {
        self.x += other.x
        self.y += other.y

        // other.x = 1000 // ERROR: `other` is not mutable
    }
}

fn pointless() {
    let origin: Point = Point(x: 0, y: 0)
    // origin.x = 10 // ERROR: origin is not mutable

    let x: u32 = 10
    // x_coord = 100 // ERROR: `x_coord` is not mutable
    let mut y: u32 = 0
    y = 10 // OK

    let mut p: Point = origin // copies `origin`
    p.x = 10 // OK, doesn't modify `origin`

    let mut q: Point = p // copies `p`
    q.x = 100            // doesn't modify `p`

    p.add(q)
    assert p.x == 110
}
```

Note that, in this release, primitive type function parameters
can't be `mut`. This restriction might be lifted in a future release.

For example:
```fe,ignore
fn increment(mut x: u256) { // ERROR: primitive type parameters can't be mut
    x += 1
}
```
