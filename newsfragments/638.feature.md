Features that read and modify state outside of contracts are now implemented on a struct 
named "Context". `Context` is included in the standard library and can be imported with
`use std::context::Context`. Instances of `Context` are created by calls to public functions
that declare it in the signature or by unsafe code.

Basic example:

```
use std::context::Context

contract Foo:
    my_num: u256

    pub fn baz(ctx: Context) -> u256:
        return ctx.block_number()

    pub fn bing(self, new_num: u256) -> u256:
        self.my_num = new_num
        return self.my_num


contract Bar:

    pub fn call_baz(ctx: Context, foo_addr: address) -> u256:
        # future syntax: `let foo = ctx.load<Foo>(foo_addr)`
        let foo: Foo = Foo(ctx, foo_addr)
        return foo.baz()

    pub fn call_bing(ctx: Context) -> u256:
        # future syntax: `let foo = ctx.create<Foo>(0)`
        let foo: Foo = Foo.create(ctx, 0)
        return foo.bing(42)
```


Example with `__call__` and unsafe block:

```
use std::context::Context
use std::evm

contract Foo:

    pub fn __call__():
        unsafe:
            # creating an instance of `Context` is unsafe
            let ctx: Context = Context()
            let value: u256 = u256(bar(ctx))

            # return `value`
            evm::mstore(0, value)
            evm::return_mem(0, 32)

    fn bar(ctx: Context) -> address:
        return ctx.self_address()
```
